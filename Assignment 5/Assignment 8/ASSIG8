//KC03C9DS JOB ,'ITZEL FERNANDEZ',MSGCLASS=H
//STEP1 EXEC PGM=ASSIST
//STEPLIB DD DSN=KC02293.ASSIST.LOADLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSIN DD *
**************************************************
* ASSIGNMENT 8
*
* NAME: ITZEL FERNANDEZ
*
* PURPOSE: TO READ IN UNALIGNED DATA AND SORT IT
*
* REGISTER USAGE:
*          REG 1,12,13,14,15 -> STORAGE
*
*
**************************************************
MAIN     CSECT
*    <Linking into storage area>
         STM   14,12,12(13)
         LR    12,15
         USING MAIN,12
         LA    14,MAINSAVE
         ST    13,4(0,14)
         ST    14,8(0,13)
         LR    13,14
*    <Branching to build>
         LA    1,BLIST
         L     15,=V(BUILD)
         BALR  14,15
         XDUMP TABLE
*
         XPRNT MAINHED,51
         XPRNT HEADERS,51
         XPRNT BORDERS,51
*
*    <Branching to print>
         LA    1,PLIST
         L     15,=V(PRINT)
         BALR  14,15
*
*    <Branching to SortId>
         LA    1,SLIST
         L     15,=V(SORTID)
         BALR  14,15
*    <BRANCHING TO PRINT>
         LA    1,PLIST
         L     15,=V(PRINT)
         BALR  14,15
*    <EXIT LINKAGE>
         L     13,4(,13)
         LM    14,12,12(13)
*
         BR    14
*
         LTORG
MAINSAVE DS    18F
         ORG MAIN+((*-MAIN+31)/32)*32
TABLE    DS    20CL15
EOT      DS    C' '
CURRENT  DC    F'0'
MAINHED  DC    C' 1'
         DC    CL15' '
         DC    C'Initial Table Contents'
         DC    CL15' '
HEADERS  DC    C' '
         DC    C'ID Number'
         DC    CL3' '
         DC    C'PASSWORD'
         DC    CL4' '
         DC    C'WEEKDAY'
         DC    CL6' '
         DC    C'TIME'
         DC    CL5' '
         DC    C'ROOM'
BORDERS  DC    C' '
         DC    C'---------'
         DC    CL3' '
         DC    C'--------'
         DC    CL4' '
         DC    C'-------'
         DC    CL6' '
         DC    C'----'
         DC    CL5' '
         DC    C'----'
BLIST    DC    A(TABLE,EOT,CURRENT)
PLIST    DC    A(TABLE,EOT,CURRENT)
SLIST    DC    A(TABLE,EOT,CURRENT)
**************************************************
* SUBROUTINE: BUILD
*
* PURPOSE: TO BUILD THE TABLE
*
* REGISTERS USED:
*        STORAGE = R1,R12-R15
*        BINARY  = R5-R7
*        TABLE   = R2-R4
**************************************************
BUILD    CSECT
*  <Entry Linkage>
         STM   14,12,12(13)
         LR    12,15
         USING BUILD,12
         LA    14,BSAVE
         ST    13,4(0,14)
         ST    14,8(0,13)
         LR    13,14
         SR    5,5
*  <Loading Builds parameter list>
         LM    2,4,0(1)
***** <Read into buffer> ********************
*
LOOP     SR    5,5
         SR    6,6
         SR    7,7
         XREAD BUFFER,80
         BL    ENDLOOP
*
***** <ID STORAGE> **************************
*    <Take ID out of buffer>
         MVC   ID(7),BUFFER+1
*    <Pack ID into PID>
         PACK  PID,ID(7)
*    <Convert PID to binary and store in 5>
         CVB   5,PID
*    <Store 5 into table>
         STCM  5,B'1111',0(2)
*
***** <Password Storage> ********************
*    <Take password out of buffer>
         MVC   PASS(8),BUFFER+12
*    <Store password into table>
         MVC   4(8,2),PASS
*    <Encrypt password>
         XC    4(4,2),0(2)
         XC    8(4,2),0(2)
*
***** <Days of The Week Storage> ************
*    <Setting up character compare>
         CLC   DAY0(3),BUFFER+24
         BC    B'1000',SUN
*
         CLC   DAY1(3),BUFFER+24
         BC    B'1000',MON
*
         CLC   DAY2(3),BUFFER+24
         BC    B'1000',TUE
*
         CLC   DAY3(3),BUFFER+24
         BC    B'1000',WED
*
         CLC   DAY4(3),BUFFER+24
         BC    B'1000',THU
*
         CLC   DAY5(3),BUFFER+24
         BC    B'1000',FRI
*
         CLC   DAY6(3),BUFFER+24
         BC    B'1000',SAT
*    <Adding day of week to table>
SUN      PACK  CON0(8),=CL1'0'
         CVB   6,CON0
         SLL   6,21
         B     TIME
MON      PACK  CON1(8),=CL1'1'
         CVB   6,CON1
         SLL   6,21
         B     TIME
TUE      PACK  CON2(8),=CL1'2'
         CVB   6,CON2
         SLL   6,21
         B     TIME
WED      PACK  CON3(8),=CL1'3'
         CVB   6,CON3
         SLL   6,21
         B     TIME
THU      PACK  CON4(8),=CL1'3'
         CVB   6,CON4
         SLL   6,21
         B     TIME
FRI      PACK  CON5(8),=CL1'4'
         CVB   6,CON5
         SLL   6,21
         B     TIME
SAT      PACK  CON6(8),=CL1'5'
         CVB   6,CON6
         SLL   6,21
         B     TIME
*
***** <Time Storage> ************************
*
TIME     SR    7,7
**   <Hour Storage>
*    <Take Hour our of buffer>
         MVC   HOUR(2),BUFFER+37
*    <Pack hour into PHour>
         PACK  PHOUR(8),HOUR
*    <Convert PHOUR to binary, store in 7>
         CVB   7,PHOUR
*    <Store 5 into table>
         SLL   7,16
         XR    6,7
         SR    7,7
**   <Minute Storage>
*    <Take min out of buffer>
         MVC   MIN(2),BUFFER+40
*    <Pack min into PMin>
         PACK  PMIN,MIN
*    <Convert PMIN to binary, store in 7>
         CVB   7,PMIN
*    <Shift 7 by 20 bits>
         SLL   7,10
*    <Store into register 6>
         XR    6,7
*
***** <Room number storage area> ************
*
         SR    7,7
*    <Take room number out of buffer>
         MVC   NUM(3),BUFFER+46
*    <Pack NUM into PNUM>
         PACK  PNUM,NUM
*    <Convert PNUM to binary and store in 7>
         CVB   7,PMIN
*    <Store into register 6>
         XR    6,7
*
***** <Add all to table> ********************
*
         STCM  6,B'0111',12(2)
*        XDUMP 0(2)
         A     4,=F'1'
         LA    2,15(2)
         CR    2,3
         BC    B'1000',ENDLOOP
         B     LOOP
*
***** <Exit linkage> ************************
ENDLOOP  L     13,4(0,13)
         LM    14,12,12(13)
         BR    14
*
         LTORG
BSAVE    DS    18F
CRG      DC    C' '
BUFFER   DS    CL80
ID       DS    CL7
FILLER1  DS    D
PID      DS    PL8
PIDOUT   DS    CL8
PASS     DS    CL8
DAY0     DC    C'Sunday'
DAY1     DC    C'Monday'
DAY2     DC    C'Tuesday'
DAY3     DC    C'Wednesday'
DAY4     DC    C'Thursday'
DAY5     DC    C'Friday'
DAY6     DC    C'Saturday'
HOUR     DS    CL2
MIN      DS    CL2
FILLER2  DS    D
PMIN     DS    PL8
PHOUR    DS    D
CON0     DS    D
CON1     DS    D
CON2     DS    D
CON3     DS    D
CON4     DS    D
CON5     DS    D
CON6     DS    D
NUM      DS    CL3
SMALL    DS    CL3
PNUM     DS    D
**************************************************
* SUBROUTINE: PRINT
*
* PURPOSE: TO PRINT THE TABLE IN A PROPER FORMAT
*
* REGISTERS USED:
*        STORAGE = R1,R12-R15
*     CVD/MVC/ED = R5-R7
*          TABLE = R2-R4
*
*************************************************
PRINT    CSECT
         STM   14,12,12(13)            STD ENTRY LINKAGE
         LR    12,15
         USING PRINT,12
         LA    14,PRNSAVE
         ST    13,4(0,14)
         ST    14,8(0,13)
         LR    13,14
         LM    2,4,0(1)
         L     3,0(,3)
         MVI   PRINTLN,C' '            UP TO TOPLOP: MAKE
         MVC   PRINTLN+1(48),PRINTLN   HEADERS FOR XPRNT
         XPRNT PRINTLN,49
         MVC   PRINTLN(2),=CL2'0 '
         MVC   PRINTLN+2(47),PRINTLN+1
         MVC   PRINTLN+11(24),0(4)
         XPRNT PRINTLN,49
         MVC   PRINTLN(27),=CL27'-ID Number   Password   Day'
         MVC   PRINTLN+27(22),=CL22'           Time   Room'
         XPRNT PRINTLN,49
TOPLOP   MVC   PRINTLN(4),=CL4'0 R '   CLEAR OUT THE LINE
         MVC   PRINTLN+4(45),PRINTLN+3
         ICM   4,B'1111',0(2)          NOW USING R4 FOR LOADER
         CVD   4,PKHOLD                CVD THE ID & UNPK
         UNPK  PRINTLN+3(7),PKHOLD+4(4)
         TR    PRINTLN+9(1),WHYC       MAKE THE LAST CHAR A #
         MVC   PRINTLN+13(8),4(2)      GET THE PASSWORD OUT
         XC    PRINTLN+13(4),0(2)      XOR THE PASSWORD
         XC    PRINTLN+17(4),0(2)
         SR    4,4                     ZERO R4
         ICM   4,B'0111',12(2)         LOAD 3BYTE PACKED DATA
         SRL   4,21                    SHIFT TO GET ONLY DAY
         C     4,=F'0'                 CONVERT THE DAY TO
         BE    SUN                     TEXT (GOES ON 'TILL
         C     4,=F'1'                 MOTHEND)
         BE    MON
         C     4,=F'2'
         BE    TUS
         C     4,=F'3'
         BE    WED
         C     4,=F'4'
         BE    THU
         C     4,=F'5'
         BE    FRI
         C     4,=F'6'
         BE    SAT
SUN      MVC   PRINTLN+25(6),=CL6'Sunday'
         B     MOTHEND
MON      MVC   PRINTLN+25(6),=CL6'Monday'
         B     MOTHEND
TUS      MVC   PRINTLN+25(7),=CL7'Tuesday'
         B     MOTHEND
WED      MVC   PRINTLN+25(9),=CL9'Wednesday'
         B     MOTHEND
THU      MVC   PRINTLN+25(8),=CL8'Thursday'
         B     MOTHEND
FRI      MVC   PRINTLN+25(6),=CL6'Friday'
         B     MOTHEND
SAT      MVC   PRINTLN+25(8),=CL8'Saturday'
MOTHEND  ICM   4,B'0111',12(2)         LOAD 3BYTE PACKED DATA
         SLL   4,11                    GET ONLY HOUR
         SRL   4,27
         CVD   4,PKHOLD                CVD HOUR & UNPK
         UNPK  PRINTLN+37(2),PKHOLD+4(4)
         TR    PRINTLN+38(1),WHYC      MAKE THE LAST CHAR A #
         MVI   PRINTLN+39,C':'         PLACE A :
         ICM   4,B'0111',12(2)         LOAD 3BYTE PACKED DATA
         SLL   4,16                    GET ONLY MIN
         SRL   4,26
         CVD   4,PKHOLD                CVD MIN & UNPK
         UNPK  PRINTLN+40(2),PKHOLD+4(4)
         TR    PRINTLN+41(1),WHYC      AKE THE LAST CHAR A #
         ICM   4,B'0111',12(2)         LOAD 3BYTE PACKED DATA
         N     4,=X'000003FF'          AND TO GET ONLY ROOM#
         CVD   4,PKHOLD                CVD ROOM# & UNPK
         UNPK  PRINTLN+46(3),PKHOLD+4(4)
         TR    PRINTLN+48(1),WHYC      AKE THE LAST CHAR A #
         XPRNT PRINTLN,49              PRINT ALL THE DATA
         LA    2,15(,2)                INCREMENT CURSOR
         CR    2,3                     CHECK TO SEE IF AT EOT
         BC    B'1010',LOPEND
         B     TOPLOP
LOPEND   DS    0H
         L     13,4(0,13)              STD EXIT LINKAGE
         LM    14,12,12(13)
         BR    14
         LTORG
PRNSAVE  DS    18F
WHYC     DC    256X'00'               THIS IS NEEDED SINCE THE
         ORG   WHYC+X'A0'             THE NUMS DONT END IN F
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         ORG   WHYC+X'C0'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         ORG   WHYC+X'E0'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         ORG
PKHOLD   DS    D                       HOLDER FOR CVD
PRINTLN  DS    CL49                    OUTPUT LINE
********************************************
* Subroutine: SORTID
*
* Desc: Sorts the table built by BUILD by ID
*       Using a buble sort
*
* Expected parameters:
* 1F: Addr of table
* 2F: Addr of EOT ptr
*
* Reg usage:
* R2 - I
* R3 - STOP
* R4 - J
* R5 - K
*
* Sort Logic:
*
*         I is a pointer initially to the first entry
*         J is a pointer initially to the first entry
*         K is a pointer
*         STOP is a pointer to the last entry
*         Temp is a variable as large as a table entry
*
*         Do While (I < STOP)
*           J = I
*           Do While (J < STOP)
*             K = J
*             Increment J
*             If Key(J) > Key(K)
*               Temp = Entry(J)
*               Entry(J) = Entry(K)
*               Entry(K) = Temp
*             EndIf
*           EndDo
*           Decrement STOP
*         EndDo
********************************************
SORTID   CSECT
         STM   14,12,12(13)            STD ENTRY LINKAGE
         LR    12,15
         USING SORTID,12
         LA    14,SORTSAV
         ST    13,4(0,14)
         ST    14,8(0,13)
         LR    13,14
         LM    2,3,0(1)
         L     3,0(,3)                 LOAD EOT PTR
         S     3,=F'15'                POINT TO LAST ENTRY
         LR    4,2                     LOAD I
LOP1     CR    2,3                           WHILE I < STOP
         BC    B'1010',LOPEND1
         LR    4,2                           J = I
LOP2     CR    4,3                           WILE J < STOP
         BC    B'1010',LOPEND2
         LR    5,4                           K = J
         LA    4,15(,4)                      INCREMENT J
SIF      CLC   0(4,4),0(5)             IF KEY(J) > KEY(K)
         BC    B'1010',SIFEND          THEN...
         MVC   TEMP(15),0(4)           TEMP = ENTRY(J)
         MVC   0(15,4),0(5)            ENTRY(J) = ENTRY(K)
         MVC   0(15,5),TEMP            ENTRY(K) = TEMP
SIFEND   B     LOP2                    ENDIF
LOPEND2  S     3,=F'15'                END DO, DECREMENT STOP
         B     LOP1                    END DO
LOPEND1  DS    0H
         L     13,4(0,13)              STD EXIT LINKAGE
         LM    14,12,12(13)
         BR    14
         LTORG
SORTSAV  DS    18F                     SORT STORAGE SPACE
TEMP     DS    15C                     SWAP VALUE
         END   MAIN
/*
//FT05F001  DD  DSN=KC02314.SPRING20.CSCI360.HW8DATA,DISP=SHR
//FT06F001   DD SYSOUT=*
//